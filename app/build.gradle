apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'com.zmengames.zenlauncher'
        minSdkVersion 15
        targetSdkVersion 28
        versionName = buildTimeStamp() + "@" + gitLastCommit()
        versionCode = gitVersion()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfigs {
            debug {
                keyAlias 'rasanen'
                keyPassword '***REMOVED***'
                storeFile file('/home/kari/AndroidStudioProjects/ANDROID_KEYSTORE')
                storePassword '***REMOVED***'
            }
            release {
                keyAlias 'rasanen'
                keyPassword '***REMOVED***'
                storeFile file('/home/kari/AndroidStudioProjects/ANDROID_KEYSTORE')
                storePassword '***REMOVED***'
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        sourceSets {
            main {
                jniLibs.srcDirs = ['libs']
            }
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    implementation 'androidx.annotation:annotation:1.0.1'


    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.apis:google-api-services-drive:v3-rev151-1.25.0'
    implementation 'com.google.http-client:google-http-client-gson:1.26.0'
    implementation('com.google.api-client:google-api-client-android:1.26.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation "me.leolin:ShortcutBadger:1.1.22@aar"
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.11"
    }
}

apply plugin: "net.ltgt.errorprone"

tasks.withType(JavaCompile) {
    options.compilerArgs += [
            '-Xep:MissingOverride:OFF',
    ]
}

configurations.errorprone {
    resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.0.21'
}
def gitVersion() {
    def process = "git rev-list HEAD --first-parent --count".execute()
    return process.text.toInteger()
}
def gitLastCommit() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.trim()
}

def buildTimeStamp() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}